// Override script of the pet Lim-Lim
// Globals to consider:
// A7-LimLimFlee::GLOBAL        When set to 1, Lim-Lim actively flees from Player1
// A7-LimLimFleeTimer::GLOBAL   When expired then A7-LimLimFlee::GLOBAL is set to 0
// A7-LimLimBerserk::GLOBAL     When set to 1 then Lim-Lim activates special berserk mode
//  - remove dialog
//  - update XP gained for the kill (Party level: <10: 1000, >= 10: 5000)
//  - replace weapon by a powerful version (Party level: <10: P1-10.ITM, >= 10: P3-12M4.ITM)
//  - update THAC0 (Party level: <10: 10, >= 10: 0)
//  - update AC (Party level: <10: 6, >= 10: -2)
//  - update Saving Throws (Party level: <10: +5, >= 10: +10)
//  - update HP (Party level: <10: 32, >= 10: 96)
//  - update APR (Party level: <10: 2, >= 10: 4)
//  - update critical hit chance (Party level: >= 10: +15%)
//  - apply Haste
//  - turn hostile
//  - AttackOneRound() randomly on any party members (Player1 is preferred)
//  - Berserk mode is active for one turn before the creature escapes forever

IF
  Global("A7-LimLimBerserk","GLOBAL",0)
  Global("init","LOCALS",0)
THEN
  RESPONSE #100
    SetGlobal("init","LOCALS",1)
    ChangeEnemyAlly(Myself,GOODBUTBLUE)
    Continue()
END

IF
  Global("A7-LimLimBerserk","GLOBAL",0)
  !Global("A7-LimLimFlee","GLOBAL",0)
  GlobalTimerExpired("A7-LimLimFleeTimer","GLOBAL")
THEN
  RESPONSE #100
    SetGlobal("A7-LimLimFlee","GLOBAL",0)
END

IF
  Global("A7-LimLimBerserk","GLOBAL",0)
  !Global("A7-LimLimFlee","GLOBAL",0)
  See(Player1)
THEN
  RESPONSE #100
    RunAwayFromNoInterruptNoLeaveArea(LastSeenBy(Myself),60)
END

IF
  Global("A7-LimLimBerserk","GLOBAL",1)
  LevelPartyLT(10)
THEN
  RESPONSE #100
    SetInterrupt(FALSE)
    SetGlobal("A7-LimLimBerserk","GLOBAL",2)
    SetGlobalTimer("A7-LimLimBerserkTimer","GLOBAL",ONE_TURN)
    SaveObjectLocation("LOCALS","A7-LimLimBerserkLoc",Player1)
    SetDialogue("")
    ChangeAIScript("a7-plbrs",DEFAULT)
    ChangeStat(Myself,XPVALUE,1000,SET)
    DestroyItem("P1-4")
    CreateItem("P1-10",0,0,0)
    XEquipItem("P1-10",Myself,SLOT_WEAPON,EQUIP)
    ApplySpellRES("a7-plb1",Myself) // perform stat upgrades
    Enemy()
    SetInterrupt(TRUE)
END

IF
  Global("A7-LimLimBerserk","GLOBAL",1)
  !LevelPartyLT(10)
THEN
  RESPONSE #100
    SetInterrupt(FALSE)
    SetGlobal("A7-LimLimBerserk","GLOBAL",2)
    SetGlobalTimer("A7-LimLimBerserkTimer","GLOBAL",ONE_TURN)
    SaveObjectLocation("LOCALS","A7-LimLimBerserkLoc",Player1)
    SetDialogue("")
    ChangeAIScript("a7-plbrs",DEFAULT)
    ChangeStat(Myself,XPVALUE,5000,SET)
    DestroyItem("P1-4")
    CreateItem("P3-12M4",0,0,0)
    XEquipItem("P3-12M4",Myself,SLOT_WEAPON,EQUIP)
    ApplySpellRES("a7-plb2",Myself) // perform stat upgrades
    Enemy()
    SetInterrupt(TRUE)
END

IF
  Global("A7-LimLimBerserk","GLOBAL",2)
  GlobalTimerExpired("A7-LimLimBerserkTimer","GLOBAL")
THEN
  RESPONSE #100
    SetGlobal("A7-LimLimBerserk","GLOBAL",3)
    EscapeAreaDestroy(6)
END
