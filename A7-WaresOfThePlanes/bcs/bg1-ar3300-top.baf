// *** BG1: Beregost area script ***

// First-time initialization of the merchant
IF
  Global("A7-InitMerchant","GLOBAL",0)
THEN
  RESPONSE #100
    SetGlobal("A7-InitMerchant","GLOBAL",1)
    CreateCreatureImpassableAllowOverlap("a7-merc",[0.0],S)
    ActionOverride("a7-merc",MakeGlobal())
    ActionOverride("a7-merc",DestroySelf())
    Continue()
END

// Merchant arrives
IF
  Global("A7-InitMerchant","GLOBAL",1)
  !GlobalTimerNotExpired("A7-TravelBeregostTimer","GLOBAL")
  Global("A7-TourBeregost","%bg1_are_prefix%3300",0)  // indicates the next destination (0=Nashkel Carnival, 1=Ulgoth's Beard)
THEN
  RESPONSE #100
    SetGlobal("A7-TourBeregost","%bg1_are_prefix%3300",1)
    SetGlobalTimer("A7-TravelTimeOut","GLOBAL",ONE_DAY)
    SetGlobalTimer("A7-TravelTimeOutForced","GLOBAL",TWO_DAYS)
    SetGlobalTimer("A7-TravelNashkelTimer","GLOBAL",THREE_DAYS)
    SetGlobalTimer("A7-TravelBeregostTimer","GLOBAL",SIX_DAYS)
    SetGlobalTimer("A7-TravelUBeardTimer","GLOBAL",NINE_DAYS)
    MoveGlobal("%bg1_are_prefix%3300","a7-merc",[1440.2900])
    ActionOverride("a7-merc",Face(SE))
    Continue()
END

IF
  Global("A7-InitMerchant","GLOBAL",1)
  !GlobalTimerNotExpired("A7-TravelBeregostTimer","GLOBAL")
  !Global("A7-TourBeregost","%bg1_are_prefix%3300",0) // indicates the next destination (0=Nashkel Carnival, 1=Ulgoth's Beard)
THEN
  RESPONSE #100
    SetGlobal("A7-TourBeregost","%bg1_are_prefix%3300",0)
    SetGlobalTimer("A7-TravelTimeOut","GLOBAL",ONE_DAY)
    SetGlobalTimer("A7-TravelTimeOutForced","GLOBAL",TWO_DAYS)
    SetGlobalTimer("A7-TravelUBeardTimer","GLOBAL",THREE_DAYS)
    SetGlobalTimer("A7-TravelBeregostTimer","GLOBAL",SIX_DAYS)
    SetGlobalTimer("A7-TravelNashkelTimer","GLOBAL",NINE_DAYS)
    MoveGlobal("%bg1_are_prefix%3300","a7-merc",[1440.2900])
    ActionOverride("a7-merc",Face(SE))
    Continue()
END
