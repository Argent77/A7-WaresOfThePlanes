// Item: Fiendblight +2

INCLUDE ~%MOD_FOLDER%/lib/functions.tph~

OUTER_SPRINT itm_resref ~a7-sw04~

ACTION_IF (has_sod || has_bg2) BEGIN
  // Installing item
  COPY ~%MOD_FOLDER%/itm/%itm_resref%.itm~ ~override~
    LPF update_item_generic
      STR_VAR
        itm_resref
      RET
        icon_resref
        desc_image_resref
    END

  // Installing associated resources
  LAF install_item_resources_generic
  STR_VAR
    itm_resref
    icon_resref
    desc_image_resref
  END

  // Installing additional resources
  ACTION_FOR_EACH file IN ~%itm_resref%a.spl~ ~%itm_resref%b.spl~ BEGIN
    ACTION_IF (NOT FILE_EXISTS_IN_GAME ~%file%~) BEGIN
      LAF EXT_OF_FILESPEC STR_VAR filespec = EVAL ~%file%~ RET ext END
      COPY ~%MOD_FOLDER%/%ext%/%file%~ ~override~
    END
  END

  WITH_TRA ~%MOD_FOLDER%/tra/english/%itm_resref%.tra~ ~%MOD_FOLDER%/tra/%LANGUAGE%/%itm_resref%.tra~ BEGIN
    OUTER_SPRINT file ~%itm_resref%c.spl~
    ACTION_IF (NOT FILE_EXISTS_IN_GAME ~%file%~) BEGIN
      LAF EXT_OF_FILESPEC STR_VAR filespec = EVAL ~%file%~ RET ext END
      COPY ~%MOD_FOLDER%/%ext%/%file%~ ~override~
        LPF ALTER_SPELL_EFFECT
          INT_VAR
            match_opcode = 139  // Display String
            parameter1 = RESOLVE_STR_REF(@20) // Frenzied
        END
    END
  END
END
