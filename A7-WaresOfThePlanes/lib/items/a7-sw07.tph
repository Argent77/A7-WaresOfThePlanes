// Item: Morrow's End +5

ACTION_IF (has_bg2) BEGIN

INCLUDE ~%MOD_FOLDER%/lib/functions.tph~

OUTER_SPRINT itm_resref ~a7-sw07~

// Installing item
COPY ~%MOD_FOLDER%/itm/%itm_resref%.itm~ ~override~
  LPF update_item_generic
    STR_VAR
      itm_resref
    RET
      icon_resref
      desc_image_resref
  END

// Installing associated resources
LAF install_item_resources_generic
STR_VAR
  itm_resref
  icon_resref
  desc_image_resref
END

// Installing additional resources
// Appending SPLPROT.2DA
LAF get_splprot_entry
  INT_VAR
    stat = IDS_OF_SYMBOL(~stats~ ~PROTECTION_FROM_EVIL~)
    value = 1
    relation = 1
  STR_VAR
    name = ~*_STAT(PROTECTION_FROM_EVIL)=1~
  RET
    splprot_pfe = index
END
LAF get_splprot_entry
  INT_VAR
    stat = IDS_OF_SYMBOL(~stats~ ~LEVEL_DRAIN_IMMUNITY~)
    value = 0
    relation = 1
  STR_VAR
    name = ~*_STAT(LEVEL_DRAIN_IMMUNITY)=0~
  RET
    splprot_ldi = index
END

COPY_EXISTING ~%itm_resref%.itm~ ~override~
  // add immunity to "Protected from Evil" effect
  LPF alter_item_effect_ex
    INT_VAR
      check_globals = 1
      match_opcode = 318  // Protection from resource
      match_parameter2 = 1
      parameter2 = splprot_pfe
  END
BUT_ONLY

COPY ~%MOD_FOLDER%/eff/%itm_resref%a.eff~ ~override~
     ~%MOD_FOLDER%/spl/%itm_resref%b.spl~ ~override~
     ~%MOD_FOLDER%/spl/%itm_resref%d.spl~ ~override~

COPY ~%MOD_FOLDER%/spl/%itm_resref%c.spl~ ~override~
  // add "Level Drain Immunity" filter
  LPF alter_item_effect_ex
    INT_VAR
      check_headers = 1
      match_opcode = 326  // Apply effects list
      match_parameter2 = 1
      parameter2 = splprot_ldi
  END

END
