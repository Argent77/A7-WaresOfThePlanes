// Item: Cowl of Deepest Shadows

INCLUDE ~%MOD_FOLDER%/lib/functions.tph~

OUTER_SPRINT itm_resref ~a7-ck01~

// Installing item
COPY ~%MOD_FOLDER%/itm/%itm_resref%.itm~ ~override~
  LPF update_item_generic
    STR_VAR
      itm_resref
    RET
      icon_resref
      desc_image_resref
  END

// Installing associated resources
LAF install_item_resources_generic
STR_VAR
  itm_resref
  icon_resref
  desc_image_resref
END

// Appending TOOLTIP table
COPY_EXISTING ~spwi607.spl~ ~override~
  READ_LONG NAME1 mislead_strref
BUT_ONLY
OUTER_SPRINT tooltip_line ~%itm_resref% %mislead_strref% -1 -1~
ACTION_TO_UPPER ~tooltip_line~
APPEND ~tooltip.2da~ ~%tooltip_line%~

// Appending SPLPROT table
COPY_EXISTING ~splprot.2da~ ~override~
  PATCH_IF (INDEX_BUFFER(~\b0x106[ %TAB%]+-1[ %TAB%]+7\b~) < 0) BEGIN
    // add new entry
    COUNT_2DA_ROWS 1 num_rows
    SET splprot_idx = num_rows - 3
    SPRINT line ~%WNL%%splprot_idx%_AREATYPEBITS>=n   0x106   -1    7~
    SET ofs = BUFFER_LENGTH
    SET len = STRING_LENGTH ~%line%~
    INSERT_BYTES ofs len
    WRITE_ASCIIE ofs ~%line%~ (len)
    PRETTY_PRINT_2DA
  END ELSE BEGIN
    // reuse existing entry
    READ_2DA_ENTRIES_NOW ~splprot~ 1
    FOR (row = 3; row < splprot; ++row) BEGIN
      READ_2DA_ENTRY_FORMER ~splprot~ row 1 stat
      PATCH_IF (IS_AN_INT ~stat~ && stat = 0x106) BEGIN
        READ_2DA_ENTRY_FORMER ~splprot~ row 2 value
        READ_2DA_ENTRY_FORMER ~splprot~ row 3 relation
        PATCH_IF (IS_AN_INT ~value~ && value = "-1" && IS_AN_INT ~relation~ && relation = 7) BEGIN
          SET splprot_idx = row - 3
          SET row = splprot
        END
      END
    END
  END
BUT_ONLY

// Appending STATDESC table
<<<<<<<< .../inlined/A7-WaresOfThePlanes/a7-ck01/statdesc-append.2da
%statdesc_idx1%   %statdesc_tra_20%   A7-S509A
%statdesc_idx2%   %statdesc_tra_21%   A7-S509B
>>>>>>>>
COPY_EXISTING ~statdesc.2da~ ~override~
  READ_2DA_ENTRIES_NOW ~statdesc~ 1
  SET statdesc_idx1 = 0
  FOR (row = 3; row < statdesc; ++row) BEGIN
    READ_2DA_ENTRY_FORMER ~statdesc~ row 0 index
    PATCH_IF (IS_AN_INT ~index~ && index > statdesc_idx1) BEGIN
      SET statdesc_idx1 = index
    END
  END
  SET statdesc_idx1 += 1
  SET statdesc_idx2 = statdesc_idx1 + 1
  LPF get_tra_paths STR_VAR tra_res = EVAL ~%itm_resref%~ RET tra_language tra_fallback END
  PATCH_WITH_TRA ~%tra_fallback%~ ~%tra_language%~ BEGIN
    SET statdesc_tra_20 = RESOLVE_STR_REF(@20)  // Light Sensitive
    SET statdesc_tra_21 = RESOLVE_STR_REF(@21)  // Darkvision
  END
  APPEND_FILE_EVALUATE TEXT ~.../inlined/A7-WaresOfThePlanes/a7-ck01/statdesc-append.2da~
  PRETTY_PRINT_2DA
BUT_ONLY

// Installing additional resources
COPY ~%MOD_FOLDER%/spl/%itm_resref%a.spl~ ~override~
  LPF alter_item_effect_ex
    INT_VAR
      check_headers = 1
      match_opcode = 326  // Apply effects list
      parameter2 = splprot_idx
  END

COPY ~%MOD_FOLDER%/spl/%itm_resref%c.spl~ ~override~
  LPF alter_item_effect_ex
    INT_VAR
      check_headers = 1
      match_opcode = 142  // Display portrait icon
      parameter2 = statdesc_idx1
  END

COPY ~%MOD_FOLDER%/spl/%itm_resref%d.spl~ ~override~
  LPF alter_item_effect_ex
    INT_VAR
      check_headers = 1
      match_opcode = 142  // Display portrait icon
      parameter2 = statdesc_idx2
  END

ACTION_FOR_EACH file IN ~%itm_resref%a.eff~
                        ~%itm_resref%b.spl~
                        ~a7-s509a.bam~
                        ~a7-s509b.bam~
BEGIN
  ACTION_IF (NOT FILE_EXISTS_IN_GAME ~%file%~) BEGIN
    LAF EXT_OF_FILESPEC STR_VAR filespec = EVAL ~%file%~ RET ext END
    COPY ~%MOD_FOLDER%/%ext%/%file%~ ~override~
  END
END
