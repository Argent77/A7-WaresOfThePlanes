// Installing custom creature animations

INCLUDE ~%MOD_FOLDER%/lib/find_free_anim_slot.tph~

// Ebb Creekknees
OUTER_SPRINT anim_name ~EBB_CREAKKNEES_PST~
OUTER_SPRINT ini_resref ~monster_icewind-ebb_creakknees~
LAF FIND_FREE_ANIM_SLOT STR_VAR type = ~monster_icewind~ RET anim_slot = slot END

ACTION_IF (anim_slot >= 0xe000) BEGIN
  OUTER_PATCH ~~ BEGIN
    SPRINTF slot_hex ~%x~ (anim_slot)
    INNER_PATCH_SAVE slot_hex ~%slot_hex%~ BEGIN DELETE_BYTES 0 2 END
    WHILE (STRING_LENGTH ~%slot_hex%~ < 4) BEGIN SPRINT slot_hex ~0%slot_hex%~ END
  END

  // installing INI file
  COPY ~%MOD_FOLDER%/ini/%ini_resref%.ini~ ~override/%slot_hex%.ini~

  // installing BAM files
  ACTION_FOR_EACH suffix IN ~a1~ ~a2~ ~ca~ ~de~ ~gh~ ~gu~ ~sc~ ~sd~ ~sl~ ~sp~ ~tw~ ~wk~ BEGIN
    COPY ~%MOD_FOLDER%/bam/a7-c%suffix%.bam~ ~override~
         ~%MOD_FOLDER%/bam/a7-c%suffix%e.bam~ ~override~
  END

  // installing sound files
  ACTION_FOR_EACH suffix IN ~atk1~ ~atk2~ ~die1~ ~die2~ ~die3~ ~hit1~ ~hit2~ ~hit3~ BEGIN
    COPY ~%MOD_FOLDER%/wav/a7-c%suffix%.wav~ ~override~
  END

  // adding ANIMATE.IDS entry
  APPEND ~animate.ids~ ~0x%slot_hex% %anim_name%~ UNLESS ~%anim_name%~
END ELSE BEGIN
  WARN ~WARNING: Could not install custom creature animation: %anim_name%. Using default animation.~
END


// Lim-Lim
OUTER_SPRINT anim_name ~LIM_LIM_PST~
OUTER_SPRINT ini_resref ~monster-lim_lim~
LAF FIND_FREE_ANIM_SLOT STR_VAR type = ~monster~ RET anim_slot = slot END

ACTION_IF (anim_slot >= 0x7000) BEGIN
  OUTER_PATCH ~~ BEGIN
    SPRINTF slot_hex ~%x~ (anim_slot)
    INNER_PATCH_SAVE slot_hex ~%slot_hex%~ BEGIN DELETE_BYTES 0 2 END
    WHILE (STRING_LENGTH ~%slot_hex%~ < 4) BEGIN SPRINT slot_hex ~0%slot_hex%~ END
  END

  // installing INI file
  COPY ~%MOD_FOLDER%/ini/%ini_resref%.ini~ ~override/%slot_hex%.ini~

  // installing BAM files
  ACTION_FOR_EACH suffix IN ~g1~ ~g11~ ~g12~ ~g13~ ~g14~ ~g15~ ~g2~ ~g21~ ~g22~ ~g23~ ~g24~ ~g25~ ~g26~ BEGIN
    COPY ~%MOD_FOLDER%/bam/a7-l%suffix%.bam~ ~override~
  END

  // installing sound files
  ACTION_FOR_EACH suffix IN ~1~ ~2~ ~3~ ~4~ ~6~ ~7~ ~8~ BEGIN
    COPY ~%MOD_FOLDER%/wav/a7-lim%suffix%.wav~ ~override~
  END

  // adding ANIMATE.IDS entry
  APPEND ~animate.ids~ ~0x%slot_hex% %anim_name%~ UNLESS ~%anim_name%~
END ELSE BEGIN
  WARN ~WARNING: Could not install custom creature animation: %anim_name%. Using default animation.~
END

CLEAR_IDS_MAP
